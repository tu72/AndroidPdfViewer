plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 34
    namespace 'com.github.tu72.pdfviewer'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 34
        versionCode 1
        versionName "3.2.0-beta.1"
    }
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '28.0.0'
                }
            }
        }
    }
}

dependencies {
    implementation 'com.android.support:support-compat:28.0.0'
    api 'com.github.barteksc:pdfium-android:1.9.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.tu72'
                artifactId = 'AndroidPdfViewer'
                version = '1.0'

                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

                pom {
                    name = 'AndroidPdfViewer'
                    description = 'A fork of AndroidPdfViewer'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'tu72'
                            name = 'Your Name'
                            email = 'your.email@example.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/tu72/AndroidPdfViewer.git'
                        developerConnection = 'scm:git:ssh://github.com/tu72/AndroidPdfViewer.git'
                        url = 'https://github.com/tu72/AndroidPdfViewer/tree/main'
                    }
                }
            }
        }
    }
}

// This task ensures the AAR is built before publishing
tasks.withType(PublishToMavenRepository) {
    dependsOn('assembleRelease')
}

